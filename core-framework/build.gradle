plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk 30

    defaultConfig {
        minSdk 21
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            def integrationTestPropKey = "INTEGRATION_TEST_API_KEY"
            buildConfigField("String", integrationTestPropKey, "\"${retrieveLocalStringProperty(integrationTestPropKey)}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

String retrieveLocalStringProperty(String propertyKey) {
    def localProperties = new Properties()

    rootProject.file("local.properties")
        .withInputStream(localProperties::load)

    return localProperties[propertyKey] ?: ""
}

dependencies {
    implementation project(":core")

    implementation 'javax.inject:javax.inject:1'

    def moshi_version = "1.12.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    def retrofit_version = "2.9.0"
    api "com.squareup.retrofit2:retrofit:$retrofit_version" //'api' since Retrofit generated interfaces are inter-module
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:4.9.1"

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.30'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'

    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha03"

    implementation 'androidx.core:core-ktx:1.6.0'

    def room_version = "2.3.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Tests
    testImplementation 'junit:junit:4.13.2'
//    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
    testImplementation 'io.mockk:mockk:1.12.0'


    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}