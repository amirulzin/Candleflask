plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk androidProps.targetSdk

    defaultConfig {
        minSdk androidProps.minSdk
        targetSdk androidProps.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            def integrationTestPropKey = "INTEGRATION_TEST_API_KEY"
            buildConfigField("String", integrationTestPropKey, "\"${retrieveLocalStringProperty(integrationTestPropKey)}\"")
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

String retrieveLocalStringProperty(String propertyKey) {
    def localProperties = new Properties()

    rootProject.file("local.properties")
        .withInputStream(localProperties::load)

    return localProperties[propertyKey] ?: ""
}

dependencies {
    implementation project(":core")

    //Android
    implementation "androidx.core:core-ktx:$commonLibVersions.androidCoreKtx"
    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha03"

    kapt "com.google.dagger:hilt-compiler:$commonLibVersions.hilt"
    implementation "com.google.dagger:hilt-android:$commonLibVersions.hilt"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:$commonLibVersions.moshi"
    implementation "com.squareup.moshi:moshi:$commonLibVersions.moshi"
    implementation "com.squareup.moshi:moshi-adapters:$commonLibVersions.moshi"

    //'api' as Retrofit generated interfaces are inter-module
    api "com.squareup.retrofit2:retrofit:$commonLibVersions.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$commonLibVersions.retrofit"
    implementation "com.squareup.okhttp3:okhttp:$commonLibVersions.okhttp"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$commonLibVersions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$commonLibVersions.coroutinesCore"


    def room_version = "2.3.0"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Tests
    testImplementation "junit:junit:$commonTestVersions.junit"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$commonTestVersions.mockitoKotlin"
}